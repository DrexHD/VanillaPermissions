plugins {
    id 'java'
    id 'maven-publish'
    id 'fabric-loom' version '1.0-SNAPSHOT' apply false
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

allprojects {
    apply plugin: "maven-publish"
    apply plugin: "fabric-loom"

    java {
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"
        it.options.release.set(17)
    }

    version = rootProject.version
    group = rootProject.group

    repositories {
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://jitpack.io' } // MixinExtras
        mavenCentral()
    }

    dependencies {
        modImplementation("net.fabricmc:fabric-loader:${rootProject.loader_version}")
        mappings loom.officialMojangMappings()

        implementation "com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras_version}"
        annotationProcessor("com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras_version}")

        modImplementation "me.lucko:fabric-permissions-api:${rootProject.fabric_permissions_version}"
        modImplementation "com.github.Fallen-Breath:conditional-mixin:${rootProject.conditional_mixin_version}"

        modImplementation "net.luckperms:api:${rootProject.luckperms_version}"

    }


    processResources {
        inputs.property "version", version

        filesMatching("fabric.mod.json") {
            expand "version": version
        }
    }
}

subprojects {
    dependencies {
        implementation(rootProject) {
            exclude group: "net.fabricmc", module: "fabric-loader" // prevent duplicate fabric-loader on run
        }
    }

}

subprojects.each {
	remapJar.dependsOn("${it.path}:remapJar")
}

dependencies {
	// Common code version
	minecraft("com.mojang:minecraft:${rootProject.minecraft_version}")

    implementation include("com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras_version}")
    annotationProcessor("com.github.LlamaLad7:MixinExtras:${rootProject.mixin_extras_version}")

    modImplementation include("me.lucko:fabric-permissions-api:${rootProject.fabric_permissions_version}")

    modImplementation include("com.github.Fallen-Breath:conditional-mixin:${rootProject.conditional_mixin_version}")

	subprojects.each {
		include project("${it.name}:")
	}
}

loom {
    accessWidenerPath.set(file("src/main/resources/vanilla-permissions.accesswidener"))
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}
